#include "environment.kek"

enum Tag {
	INTEGER, STRING, SYMBOL, CONS, TRUE, FALSE, NIL, VOID, B_SYNTAX, B_FUNCTION, U_FUNCTION, VECTOR, PORT
}

class EvalError : Exception {
	var obj
	EvalError(msg, obj) {
		super(msg)
		this.obj = obj
	}
}

class ArgumentNumError : Exception {
	var expected, given
	ArgumentNumError(ex, gi) {
		super("Wrong number of arguments: ")
		expected = ex
		given = gi
	}
}

class scmObject {
	var tag

	scmObject(tag) {
		this.tag = tag
	}

	toString() {
		return G.tagName[tag]
	}

	print(out) {
		out.writeln(toString())
	}

	eval(env) {
		return this
	}
}

class scmInteger : scmObject {
	var val

	scmInteger(val) {
		super(Tag.INTEGER)
		this.val = val
	}

	print(out) {
		out.writeln(String.fromInt(val))
	}
}

class scmString : scmObject {
	var str

	scmString(str) {
		super(Tag.STRING)
		this.str = str
	}

	print(out) {
		out.writeln("\"" + str + "\"")
	}
}

class scmSymbol : scmObject {
	var symName
	static var symTable
	static var symCount

	static scmSymbol() {
		symTable = []
		symCount = 0
	}

	scmSymbol(symName) {
		super(Tag.SYMBOL)
		this.symName = symName
	}

	static findSym(symName) {
		var i
		for (i = 0; i < symCount; i++) {
			if (symTable[i].symName == symName) {
				return symTable[i]
			}
		}
		return nil
	}

	static addSym(sym) {
		symTable[symCount] = sym
		symCount++
		return sym
	}

	static getSym(symName) {
		var sym = findSym(symName)
		if (sym != nil) return sym
		return addSym(new scmSymbol(symName))
	}

	toString() {
		return symName
	}

	print(out) {
		out.writeln(symName)
	}

	eval(env) {
		return env.get(this)
	}
}

class scmCons : scmObject {
	var car, cdr

	scmCons(car, cdr) {
		super(Tag.CONS)
		this.car = car
		this.cdr = cdr
	}

	print(out) {
		var obj = this
		out.writeln("(")
		while (1) {
			if (obj.tag != Tag.CONS) {
				out.writeln(". ")
				obj.print(out)
				out.writeln(")")
				return
			}

			obj.car.print(out)
			obj = obj.cdr
			if (obj != G.scmNil) {
				out.writeln(" ")
			}
			else break
		}
		out.writeln(")")
	}

	eval(env) {
		var fnVal = car.eval(env)
		var args, fargs, al, i
		var bl

		switch (fnVal.tag) {
		case Tag.B_SYNTAX:
			return fnVal.eval(env, cdr)
		case Tag.B_FUNCTION: {
			args = []
			i = 0
			al = cdr
			while (al != G.scmNil) {
				args[i] = al.car.eval(env)
				al = al.cdr
				i++
			}
			return fnVal.eval(args)
		}
		case Tag.U_FUNCTION: {
			var fnEnv = new Environment()
			fnEnv.parentEnv = fnVal.defEnv
			fargs = fnVal.argList
			args = cdr

			while (fargs != G.scmNil) {
				if (args == G.scmNil) {
					throw new EvalError("Not enough parameters ", cdr)
				}
				fnEnv.set(fargs.car, args.car.eval(env))
				fargs = fargs.cdr
				args = args.cdr
			}
			if (args != G.scmNil) {
				throw new EvalError("Too many parameters ", cdr)
			}
			for (bl = fnVal.bodyList; bl.cdr != G.scmNil; bl = bl.cdr) {
				bl.car.eval(fnEnv)
			}
			return bl.car.eval(fnEnv) // Tail call optimization
		}
		default:
			throw new EvalError("Expected function or syntax ", fnVal)
		}
	}
}

class scmVector : scmObject {
	var elems
	scmVector(elems) {
		super(Tag.VECTOR)
		this.elems = elems
	}

	print(out) {
		var i
		out.writeln("#(")
		for (i = 0; i < elems.length(); i++) {
			if (i > 0) out.writeln(" ")
			elems[i].print(out)
		}
		out.writeln(")")
	}
}

class scmFilePort : scmObject {
	var name
	var handle
	var portType
	scmFilePort(fname, mode) {
		super(Tag.PORT)
		name = fname
		handle = new File(fname, mode)
		if (mode[0] == "r") portType = "#<input-port:"
		else portType = "#<output-port:"
	}

	toString() {
		return portType + name + ">"
	}
}

class scmUserFunction : scmObject {
	var argList, bodyList, defEnv

	scmUserFunction(al, bl, de) {
		super(Tag.U_FUNCTION)
		argList = al
		bodyList = bl
		defEnv = de
	}
}

class G {
	static var scmTrue, scmFalse, scmNil, scmVoid
	static var tagName
	
	static G() {
		tagName = ["INT", "STR", "SYM", "CONS", "#t", "#f", "()", "",
				"#<syntax>", "#<function>", "#<function>", "VECTOR", "PORT"]
		scmTrue = new scmObject(Tag.TRUE)
		scmFalse = new scmObject(Tag.FALSE)
		scmNil = new scmObject(Tag.NIL)
		scmVoid = new scmObject(Tag.VOID)
	}
}
